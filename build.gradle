import com.bmuschko.gradle.docker.tasks.container.DockerCopyFileFromContainer

import de.undercouch.gradle.tasks.download.Download

import groovy.json.JsonSlurper

import java.time.format.DateTimeFormatter

plugins {
	id "com.avast.gradle.docker-compose" version "0.17.10"
}

static List toList(String s) {
	return s.trim().split(",").grep()
}

String[] databasePartitioningCompatibleServiceNames = ["mysql", "postgres"]
File projectDir = project.projectDir as File

class Config {
	public int clusterNodes = 0
	public List<String> composeFiles = new ArrayList<String>()
	public String databaseName = "lportal"
	public boolean databasePartitioningEnabled = false
	public String dataDirectory = "data"
	public List<String> hotfixURLs = new ArrayList<String>()
	public String liferayDockerImageId = ""
	public String namespace = "lrswde"
	public List<String> services = new ArrayList<String>()

	@Override
	public String toString() {

		return """

Config:
------------------------
clusterNodes: ${clusterNodes}
composeFiles: ${composeFiles}
databaseName: ${databaseName}
databasePartitioningEnabled: ${databasePartitioningEnabled}
dataDirectory: ${dataDirectory}
hotfixURLs: ${hotfixURLs}
liferayDockerImageId: ${liferayDockerImageId}
namespace: ${namespace}
services: ${services}

"""
	}
}

Config config = new Config()

config.composeFiles.add("docker-compose.yaml")

Integer clusterNodes = project.getProperty("lr.docker.environment.cluster.nodes") as Integer
def composeFiles = toList(project.getProperty("lr.docker.environment.compose.files"))
String databaseName = project.getProperty("lr.docker.environment.database.name")
boolean databasePartitioningEnabled = project.getProperty("lr.docker.environment.database.partitioning.enabled").toBoolean()
String dataDirectory = project.getProperty("lr.docker.environment.data.directory")
List hotfixURLs = toList(project.getProperty("lr.docker.environment.hotfix.urls"))
String namespace = project.getProperty("lr.docker.environment.namespace")
List services = project.properties.findAll {
	it.key =~ /^lr.docker.environment.service.enabled\[\w+\]$/
}.findAll {
	it.value =~ /true|1/
}.collect {
	it.key.substring(it.key.indexOf("[") + 1, it.key.indexOf("]"))
}

if (clusterNodes != null) {
	config.clusterNodes = clusterNodes
}

if (databaseName != null) {
	config.databaseName = databaseName
}

config.databasePartitioningEnabled = databasePartitioningEnabled

if (dataDirectory != null && dataDirectory.length() > 0) {
	config.dataDirectory = dataDirectory
}

if (!hotfixURLs.isEmpty()) {
	config.hotfixURLs = hotfixURLs
}

if (namespace != null) {
	config.namespace = namespace
}

if (!services.isEmpty()) {
	config.services = services
}

config.liferayDockerImageId = "${config.namespace.toLowerCase()}-liferay"

gradle.liferayWorkspace {
	dockerImageId = config.liferayDockerImageId
}

boolean useLiferay = config.services.contains("liferay")

boolean useClustering = useLiferay && config.clusterNodes > 0

ConfigurableFileTree dockerComposeFileTree = project.fileTree(projectDir) {
	include "**/service.*.yaml"

	if (useClustering) {
		include "**/clustering.*.yaml"
	}

	if (useLiferay) {
		include "**/liferay.*.yaml"
	}

	if(databasePartitioningEnabled) {
		if (!config.services.any {databasePartitioningCompatibleServiceNames.contains(it)}) {
			throw new GradleException("Database partitioning must be used with one of these databases: ${databasePartitioningCompatibleServiceNames}")
		}

		include "**/database-partitioning.*.yaml"
	}
}

List<String> serviceComposeFiles = config.services.collect {
	String serviceName ->

	FileTree matchingFileTree = dockerComposeFileTree.matching {
		include "**/*.${serviceName}.yaml"
	}

	if (matchingFileTree.isEmpty()) {
		List<String> possibleServices = dockerComposeFileTree.findAll{
			it.name.startsWith("service.")
		}.collect {
			it.name.substring("service.".length(), it.name.indexOf(".yaml"))
		}

		throw new GradleException(
			"The service '${serviceName}' does not have a matching service.*.yaml file. Possible services are: ${possibleServices}");
	}

	matchingFileTree.getFiles()
}.flatten().collect {
	projectDir.relativePath(it)
}

config.composeFiles.addAll(serviceComposeFiles)

tasks.register("exportContainerData", DockerCopyFileFromContainer) {
	containerId = "${config.namespace}-data-helper"

	doFirst {
		mkdir hostPath.get()
	}

	hostPath = provider {
		DateTimeFormatter formatter = DateTimeFormatter.
		ofPattern("yyyyMMdd.HHmmss").
		withZone(ZoneId.systemDefault())

		"exported_data/data_${config.namespace}_${formatter.format(Instant.now())}"
	}

	remotePath = "/data/container-data"
}

tasks.register("prepareHotfixes", Download) {
	dest project.layout.dir(project.provider {project.file("configs/common/patching")})

	onlyIf {
		!config.hotfixURLs.isEmpty()
	}

	src config.hotfixURLs
}

tasks.register("shareWorkspace", Zip) {
	archiveFileName = provider {
		DateTimeFormatter formatter = DateTimeFormatter.
		ofPattern("yyyyMMdd.HHmmss").
		withZone(ZoneId.systemDefault())

		"workspace_${config.namespace}_${formatter.format(Instant.now())}.zip"
	}

	destinationDirectory = file 'shared_workspaces'

	doLast {
		logger.lifecycle "\nWorkspace zip: ${archiveFile.get()}\n"
	}

	exclude ".gitkeep"
	exclude ".gitignore"
	exclude ".git"
	exclude ".gradle"
	exclude "shared_workspaces"
	exclude {
		it.relativePath.pathString.startsWith("exported_data/") &&
		!it.relativePath.pathString.startsWith(config.dataDirectory)
	}

	from '.'

	include "${config.dataDirectory}/**"
	include "**/*"

	outputs.upToDateWhen {
		false
	}
}

clean {
	dependsOn ":cleanPrepareHotfixes"
}

dockerDeploy {
	if (useLiferay) {
		dependsOn ":prepareHotfixes"
	}
}

composeUp {
	if (useLiferay) {
		dependsOn ":buildDockerImage"
	}

	doFirst {
		println "Using config: ${config}"
	}

	onlyIf {
		!config.composeFiles.isEmpty()
	}
}

Map<String, Object> environmentMap = [:]

environmentMap.put "DATA_DIRECTORY", config.dataDirectory
environmentMap.put "DATABASE_NAME", config.databaseName
environmentMap.put "NAMESPACE", config.namespace

if (useClustering) {
	environmentMap.put "LIFERAY_CLUSTER_NODES", config.clusterNodes
}

if (useLiferay) {
	environmentMap.put "LIFERAY_IMAGE_NAME", config.liferayDockerImageId
}

dockerCompose {
	// DEBUG: Set to true to print container startup output to the console
	captureContainersOutput = false

	// DEBUG: Uncomment to dump the container logs to a directory for inspection
	// captureContainersOutputToFiles = project.file('containerLogs')

	environment.putAll environmentMap

	projectName = config.namespace

	// DEBUG: Set to false to retain volume data on restart
	removeVolumes = true

	tcpPortsToIgnoreWhenWaiting = [8000, 11311]
	useComposeFiles = config.composeFiles

	// DEBUG: Set to true if container startup is failing
	retainContainersOnStartupFailure = false
}